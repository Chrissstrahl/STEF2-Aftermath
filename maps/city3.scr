//Declare, so we can call a fucntion which is on the bottom
//wting a function that is on the top

void main();
void setupLevel();
void openDoorForTelsia();
void telsiarescue();
void unlockdoor1();
void unlockdoor2();
void telsia_scream();
void telsia_thanks();
void telsia_killed();
void addWaterHud();
void addWaterHudToMe(entity eTrigger,entity ePlayer);
float isEntityInsideOfEntity(entity e1,entity e2,float fPadding);
float isBoundingBox2InsideOfBoundingBox1(vector vOrigin1,vector vBBoxMins1,vector vBBoxMaxs1,vector vOrigin2,vector vBBoxMins2,vector vBBoxMaxs2,float fPadding);
float floatNotNegative(float fNotNegative);
void makeSoldiersAttackPlayer();
void spawnExoInsideTube();


void main()
{
//make level fogy
	$world.entity_fade_dist( 3500 );//6000
	$world.farplane_cull( 0 );//1
	$world.farplane_color ( '0.0875 0.1 0.145' );
	$world.farplane ( 2500 );//4000
//setup the level objects
	thread setupLevel();
//wait for the player to spawn
	waitForPlayer();
//clear/set viewmodes avialbe for Tricorder
	$world.clearAvailableViewModes();
//give weapons from previouse level, in case player loads city3 as standalone
	$player.give("models/weapons/worldmodel-compressionrifle.tik");
	$player.give("models/weapons/worldmodel-sniperrifle.tik");
	$player.give("models/weapons/worldmodel-tricorder-stx.tik");
	$player.give("models/weapons/worldmodel-phaser-stx.tik");
//load objectives
	if(getCvarInt("g_gametype") != 1)
	{
		$player.loadobjectives( "city3" );
		wait( .1 );
		$player.setobjectiveshow( "c3findWayOut", 1 );
		$player.setobjectiveshow( "c3unlockTelsia", 0 );
	}
	wait(5);
//Save Game
	if(getCvarInt("g_gametype") != 1)
	{
		stuffCMD("ui_savegamewithname \"City3 - Serwers\"");
	}
}

void setupLevel()
{
//remove current used weapon
	$telsia.killattach();
//remove archetypes
	$terminal0.archetype("");
	$terminal1.archetype("");
}

void openDoorForTelsia()
{
	entity eTrigger,eActor,eDoor;
//get the entity that started this very instance of this function 
	eTrigger = getCurrentEntity();
//see that it exists, this function does not accept calls from console
	if(doesEntityExist(eTrigger))
	{
	//grab the entity that activated the entity that called this thread
	//in this case, due to the trigger setup it will be a player
		eActor = eTrigger.getLastActivatingEntity();
		if(doesEntityExist(eActor))
		{
		//grab door by reading a uservar of the trigger
			eDoor = getEntity( eTrigger.getStringVar("uservar1") );
		//open door if activator is not the player
			if(eActor.getTargetname() != "$player" && doesEntityExist(eDoor) == 1)
			{
			//open the door - keeps it open if it is open already
				eDoor.open(eActor);
			}
		}
	}
}


void telsiarescue()
//Munro enters the room where telsia is inside
{
//playDialog does not work with players, use: simpleplaydialog
	$player.simpleplaydialog("localization/sound/dialog/borg01/munro_telsia.mp3",2,128);
//make telsia run to the node, AI need to be off!!! (KEY=ai_off VALUE=1 -OR- $telsia.ai_off();)
	$telsia.walkto("$telsiaFfrunTo","run");
//wait for telsia to reach the node
	waitFor($telsia);
//make telsia asks munro to t6ake down the ff
	$telsia.playDialog("localization/sound/dialog/borg01/telsia_takedown.mp3",2,128,0);
}

void unlockdoor1()
//make the door unlock after using console in pool room
{
//update objectives
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectivecomplete( "c3", 1 );
	}

	$door1.unlock();
//wait for the player to reach the location
	wait(4);
//spawn dust effect
	triggerEntity($spawnRomulanRockEffect);
//wait for the effect to visualize
	wait(1);
//spawn enemy
	triggerEntity($spawnRomulanRockTrigger);
}
void unlockdoor2()
//make the door unlock after using nearby console
{
	$door2.unlock();
//open door
	$door2.open($world);
//remove the message from the trigger
	$doo2_useTrigger.message("");
//enable trigger that opens door for telsia if she is close
	$autoOpenDoorForTelsia.triggerable();
}

void telsia_scream()
//make telsia scream
{
	$telsia.headWatch($player);
	wait(0.2);
	$telsia.playDialog("localization/sound/dialog/borg01/telsia_scream.mp3",2,128,0);
	
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectiveshow( "c3unlockTelsia", 1 );
	}
}

void telsia_thanks()
//make telsia thank munro
{
//update objectives
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectivecomplete( "c3unlockTelsia", 1 );
		$player.setobjectiveshow( "c3escortTelsiaOut", 1 );
	}
//unlock gate3 so the player can now proceed with telsia
	$gate3.unlock();
//telsia says thanks
	$telsia.playDialog("localization/sound/dialog/borg01/telsia_goodjob.mp3",2,128,0);
	wait(2);
//stop telsia looking at munro
	$telsia.headwatchtarget("none",5);
//enable the ai again, so telsia will follow the player
	$telsia.ai_on();
//set the tenceny to follow munro, default is 1 but make sure it works garanteed
	$telsia.settendency("follow",1);
//now telsia can die
	$telsia.immortal(0);
//make her show in the health indication bar, make her weak, so player must stay close
	$telsia.health( 100 );
	$telsia.maxbosshealth(100);
	$telsia.updatebosshealth( 1 );
//do not allow her to be killed by any event
	$telsia.killthread("telsia_killed");
	
//do not display mission sucess hud
	nointermission();
}

void telsia_killed()
//this is started when teslia dies
{
//playDialog does not work with players, use: simpleplaydialog
	$player.simpleplaydialog("localization/sound/dialog/borg01/munro_telsia.mp3",2,128);
//fail the mission
	$telsia.failmission(2);
}

void addWaterHud()
{
	entity eTrigger,ePlayer;
//get the entity that started this very instance of this function 
	eTrigger = getCurrentEntity();
//see that it exists, this function does not accept calls from console
	if(doesEntityExist(eTrigger))
	{
	//grab the entity that activated the entity that called this thread
	//in this case, due to the trigger setup it will be a player
		ePlayer = eTrigger.getLastActivatingEntity();
		if(doesEntityExist(ePlayer))
		{
		//start a seperate function that will handle the effect hud
		//while the player is inside the trigger
			addWaterHudToMe(eTrigger,ePlayer);
		}	
	}
}

void addWaterHudToMe(entity eTrigger,entity ePlayer)
{
//if trigger and player do exist
	if( doesEntityExist(eTrigger) == 1 && doesEntityExist(ePlayer) == 1 )
	{
		if(ePlayer.getFloatVar("waterHudAdded") != 1)
		{
		//add the hud just once, reset this entity varaibale when player leaves the trigger
			ePlayer.setFloatVar("waterHudAdded",1);			
		//add hud
			ePlayer.addhud("city_effect_waterfall");
		//as long as the player is inside the trigger loop here
			while( isEntityInsideOfEntity(ePlayer,eTrigger,-8) )
			{
				wait(0.1);
			}
		//make sure the player is still on the server
			if(doesEntityExist(ePlayer))
			{
				ePlayer.removehud("city_effect_waterfall");
				ePlayer.setFloatVar("waterHudAdded",0);
			}
		}

	}
}


//taken from hzm co-op mod
float isEntityInsideOfEntity(entity e1,entity e2,float fPadding)
//------------------------------------------------------------------------------
//CHECK IF ENTITY1 IS INDIDE OF THE BOUNDINBOX OF ENTITY2 OR OTHERWISE
//------------------------------------------------------------------------------
{
//USE PADDING FOR MOVING ENTITYS, DEPENDING ON VELOCITY THEY CAN HAVE MOVED AWAY FROM THE LAST ORIGIN
	if(doesEntityExist(e1) == 1 && doesEntityExist(e2) == 1){
//CREATE VARS
		vector	vOrigin1,
				vBBoxMins1,
				vBBoxMaxs1,
				vOrigin2,
				vBBoxMins2,
				vBBoxMaxs2;
//GRAB DATA FROM BBOX, SAVES US TWO "else" statments
		vBBoxMins1 = e1.getMins();
		vBBoxMaxs1 = e1.getMaxs();
		vBBoxMins2 = e2.getMins();
		vBBoxMaxs2 = e2.getMaxs();
//GET ORIGINS
		vOrigin1 = e1.getOrigin();
		vOrigin2 = e2.getOrigin();
//DO THE BBOX CHECK, CHECK BOTHCONSTELLATIONS TO AVOID MISSCALCULATION, UPDATE isBoundingBox2InsideOfBoundingBox1 TO SOLVE ERROR
		float fInside = 0;
		if(	isBoundingBox2InsideOfBoundingBox1(vOrigin1,vBBoxMins1,vBBoxMaxs1,vOrigin2,vBBoxMins2,vBBoxMaxs2,fPadding) == 1 &&
			isBoundingBox2InsideOfBoundingBox1(vOrigin2,vBBoxMins2,vBBoxMaxs2,vOrigin1,vBBoxMins1,vBBoxMaxs1,fPadding) == 1)
		{
			fInside = 1;
		}
		return fInside;
	}
//Return False Player by default
	return 0;
}


//taken from hzm co-op mod
float isBoundingBox2InsideOfBoundingBox1(	vector vOrigin1,//ORIGIN BBOX1
															vector vBBoxMins1,//e.getMins()
															vector vBBoxMaxs1,//e.getMaxs()
															vector vOrigin2,//ORIGIN BBOX2
															vector vBBoxMins2,//e.getMins()
															vector vBBoxMaxs2,//e.getMaxs()
															float fPadding)//ADD TO BOUNDINGBOX, EXTRA SIZE (TOLLERANCE)
//------------------------------------------------------------------------------
//CHECK IF THE GIVEN BOUNDINGBOXES ARE TOUCHING OR INSIDE OF EACH OTHER BOUNDINGBOX
//------------------------------------------------------------------------------
{
//VECTOR LENGTH
	float fCalcDist;
//TEMP-VECTORS FOR CALCULATIONS
 	vector vCalc1,vCalc2;
//IF ORIGIN IS NOT EXACTLY THE SAME
	if(vOrigin1 != vOrigin2){
//X-CHECK
		vCalc2 = (vCalc1 = '0 0 0');
		vCalc1_x = vOrigin1_x;//GET ONLY X
		vCalc2_x = vOrigin2_x;//GET ONLY X
		fCalcDist = ( floor(vectorLength(vCalc1 - vCalc2)) - fPadding );
		
		if(vCalc1_x >= vCalc2_x){
			if(	fCalcDist > (floatNotNegative(vBBoxMins1_x) + vBBoxMaxs2_x)){
				return 0;//IS OUTSIDE ABORT OTHER CALCS
			}
		}
		else if(fCalcDist > (vBBoxMaxs1_x + floatNotNegative(vBBoxMins2_x))){
			return 0;//IS OUTSIDE ABORT OTHER CALCS
		}
//Y-CHECK
		vCalc2 = (vCalc1 = '0 0 0');
		vCalc1_y = vOrigin1_y;//GET ONLY Y
		vCalc2_y = vOrigin2_y;//GET ONLY Y
		fCalcDist = ( floor(vectorLength(vCalc1 - vCalc2)) - fPadding );
		
		if(vCalc1_y >= vCalc2_y){
			if(	fCalcDist > (floatNotNegative(vBBoxMins1_y) + vBBoxMaxs2_y)){
				return 0;//IS OUTSIDE ABORT OTHER CALCS
			}
		}
		else if(fCalcDist > (vBBoxMaxs1_y + floatNotNegative(vBBoxMins2_y))){
			return 0;//IS OUTSIDE ABORT OTHER CALCS
		}
//Z-CHECK
		vCalc2 = (vCalc1 = '0 0 0');
		vCalc1_z = vOrigin1_z;//GET ONLY Z
		vCalc2_z = vOrigin2_z;//GET ONLY Z
		fCalcDist = ( floor(vectorLength(vCalc1 - vCalc2)) - fPadding );
		
		if(vCalc1_z <= vCalc2_z){
			if(	fCalcDist > (vBBoxMaxs1_z + floatNotNegative(vBBoxMins2_z))){
				return 0;//IS OUTSIDE ABORT OTHER CALCS
			}
		}
		else if(fCalcDist > (floatNotNegative(vBBoxMins1_z) + vBBoxMaxs2_z)){
			return 0;//IS OUTSIDE ABORT OTHER CALCS
		}
	}
//IS STUCK
	return 1;
}

//taken from hzm co-op mod
float floatNotNegative(float fNotNegative)
//----------------------------------------------------------------------
// If negative value, convert to positive
//----------------------------------------------------------------------
{
	if(fNotNegative < 0){
		fNotNegative = (fNotNegative - (fNotNegative*2));
	}
	return fNotNegative;
}


void cinematic_shaftObserve()
//player observes informant and commander
{
//fade screen black, this level starts with a cinematic
	cam_fadeout(0.1,0,0,0,1);
	
//spawn camera entity, this level starts with a cinematic
	spawn("camera","targetname","cam1");
	
//wait for the camera entity to spawn and the cam to fade black 
	wait(0.1);

//do not do that in multiplayer, only singleplayer and solomatch
//get the current game type from the config variables of game
//0=sp,1=mp,2=solomatch
	if(getCvarInt("g_gametype") != 1)
	{
		cinematic();
		freezeplayer();
		letterbox(0.1);
	//make cam follow the given path entity
		$cam1.follow($cinematicSceneA);
	//transition to camera: cut, no transition
		$cam1.cut();
	//wait for the camera to get started moving along the path
		wait(0.1);
	//switch from player to given camera entity now
		cuecamera($cam1);
	}
//fade screen from black back in again to normal
	cam_fadeIn(0.25,0,0,0,1);
//do not allow the player to hut/push eigther of them
	$informant.nodamage();
	$commander.nodamage();
	$informant.mass(999999);
	$commander.mass(999999);
	$informant.pushable(0);
	$commander.pushable(0);
//wait for the camera to get in position
	wait(1.8);
//trigger the spawnerf to spawn a effect
	triggerEntity($cinematic_fallingDustEffect);
//wait for the camera to get in position
	wait(1.2);
//talk, I'll look for him
	$informant.playdialog("localization/sound/dialog/m10l1/info_notjob.mp3",1,256,0);
	wait(4.5);
//walk away - sewer smells, you know that
	$informant.walkto("$informantGoAway","walk");
	$commander.walkto("$commanderGoAway","walk");
//wait until they started walking away
	wait(1);
//fade screen black, this level starts with a cinematic
	cam_fadeout(0.9,0,0,0,1);
//wait until the camera has blacked out
	wait(1.1);
//do cinematic commands only in singleplayer
	if(getCvarInt("g_gametype") != 1)
	{
		noncinematic();
		clearletterbox(0.1);
		cueplayer();
		releaseplayer();
	}
//remove both actors
	$informant.remove();
	$commander.remove();
//wait until the players sees from his own location again
	wait(0.05);
//fade screen from black back in again to normal
	cam_fadeIn(0.25,0,0,0,1);
}


void makeSoldiersAttackPlayer()
{
//wait for soldier to spawn
	wait(0.05);
//make soldier attack player, in case if player is out of view
//the other soldiers shoudl follow, or wake up once this one is dead
	$soldier3.attack($player);
}

void spawnExoInsideTube()
//spawns exomorph inside the tube
//needs to modify the exomorph or it gets stuck,
//since it is originally to big
{
	float fHowMany;
	
//repeat this a few times
	for(fHowMany=6;fHowMany>0;fHowMany--)
	{
	//create entity variable
		entity eExo;
	//spawn the actor in the level, and put it into the entity variable in script
		//models/char/alien-type1a-lurker-esrb.tik
		eExo = spawn("actor","model","models/char/alien-type1a-lurker.tik","hide","1","ai_off","1","origin","10402.60 708.60 -828.00");
	//wait for the exo to spawn
		wait(0.05);
	//modify actor
		//change bounding box size, so it won't get stuck inside the tube
		eExo.setSize('-12 -12 0','12 12 22');//was:12 12 32
		//change visual size of the model
		eExo.scale(0.6);
		//change facing direction
		eExo.angle(270);
		//activate ai
		eExo.ai_on();
		//make visible again
		eExo.show();
		//make ai fall to teh ground
		//since the original bounding box made it get stuck in the wall
		//it needs a extrenal impulse to get it falling
		eExo.droptofloor();
		//in addition push it downwards once, so it hits the push trigger for sure
		eExo.setVelocity('0 0 -100');
		//wait a moment for the current exo to get pushed out of the tube
		wait(2);
	}
}



