void main();
void setupLevel();
void setupAI();
void setOnUseTheread(entity e,string s);
void actor_offAndAnim(entity eActor,string sAnimation);
void deskLightToggle();
void secretStreetLight();
void cinematic_telsiaBeamOut();
void munro_team();
void areaScanned();
void holoEmitterBlow1();
void holoEmitterBlow2();
void hologramsFlicker();
void openDoors();
void shatterWindow();
void badKilling();
void cinematic_playerSitDown();
void replicatedFood();
void replicatedFoodEat();
void openDoorToGarden();
void secretBarWakeEmUp();
void dialog_laptopuser();
void dialog_klingon2();
void dialog_blueman();
void dialog_blueman2();
void dialog_dude2();
void dialog_girl2();
void dialog_girl();
void dialog_seated();
void fail_lostTeammate();
void doorHacked();
void doorHackedUsed();
void doorHackedSpy();
void exitNow();
void changeLevel();

//global variables
float float_holoEmittersDamaged;
float float_actorIndex;
float float_playerIsAMurderer;


void main()
{
	spawn("camera","targetname","cam1");
	spawn("camera","targetname","cam2");
	$munro.ai_off();//test, delete me
	$munro.notsolid();//test, delete me
	
	$t191.unlock();
//setup the level objects
	thread setupLevel();
//setup actors
	thread setupAI();
//wait for the player to spawn
	waitForPlayer();
//clear/set viewmodes avialbe for Tricorder
	$world.clearAvailableViewModes();
	$world.addAvailableViewMode("tripwire");
	$world.addAvailableViewMode("structuralintegrity");
//give weapons from previouse level, in case player loads city4 as standalone
	$player.give("models/weapons/worldmodel-compressionrifle.tik");
	$player.give("models/weapons/worldmodel-sniperrifle.tik");
	$player.give("models/weapons/worldmodel-tricorder-stx.tik");
	$player.give("models/weapons/worldmodel-phaser-stx.tik");	
//load objectives, then inizial
	if(getCvarInt("g_gametype") != 1)
	{
		$player.loadobjectives( "city4" );
		wait( .1 );
		$player.setobjectiveshow( "c4escortTelsiaOut", 1 );
		$player.setobjectiveshow( "c4beamTelsiaBack", 1 );
		$player.setobjectiveshow( "c4locateSpy", 1 );
		$player.setobjectiveshow( "c4avoidConflict", 1 );
		$player.setobjectiveshow( "c4sitdownAnd", 0 );
		wait( .1 );
		$player.setobjectivecomplete( "c4escortTelsiaOut", 1 );
	}
}

void setupLevel()
{
//dhide puzzle area to hack the door
	$tricorderArea2.hide();
//fish swim
	$koi1.anim("idle");
	$koi1.followpath($fishSwimPath,"loop","normalangels");
//hide holo emitters
	$holoEmitter1Spark.hide();
	$holoEmitter2Spark.hide();
	$holoEmitter1.hide();
	$holoEmitter2.hide();
//setup dynamic light
	thread secretStreetLight();
//turn off
	$world.light_intensity("secretArmorLight",0);
//turn on
	$world.light_intensity("deskLight",1);
	$deskLightBeam.show();
//setup shuttle, bind parts to the pointer
	$shuttleTrigger.bind($shuttleOrigin);
	$shuttle.bind($shuttleOrigin);
	$shuttleTrigger.solid();
//remove archtypes (description when trageting objects)
	$console1.archetype("");
	$console2.archetype("");
//transporter beam in effect
	$t13.spawneffectname("TransportIn","Federation");
//replicator effect
	$t187.spawneffectname("TransportIn","FederationWeapons");
//laptop, show it opened
	$laptop.anim("open_idle");
//container
	//viewmode - this is visible on structural with tricorder only
	$container1DeckelViewmode.viewmode("structuralintegrity");
	//was set notsolid so pathmodes can connect for ai pathfinding
	$container1Deckel.solid();
}

void setupAI()
{
//Hide cinematic actor
	$munro.hide();
//TEAMMATES
	$telsia.killattach();//remove current weapon
	$telsia.allowfall(1);
	$korban.ai_off();
	$korban.allowfall(1);
	$chang.ai_off();
	$chang.allowfall(1);
	$chang.killthread("fail_lostTeammate");
	$korban.killthread("fail_lostTeammate");
//NON HOSTILES
	actor_offAndAnim($laptopuser,"bar_sitting_relaxed");
	actor_offAndAnim($blueman,"bar_sitting_forward");
	actor_offAndAnim($blueman2,"bar_sitting_forward");
	actor_offAndAnim($seated,"bar_sitting_relaxed");
	actor_offAndAnim($dude2,"bar_sitting_forward");
	actor_offAndAnim($girl,"bar_sitting_relaxed");
	actor_offAndAnim($girl2,"bar_sitting_forward");
	actor_offAndAnim($klingon1,"bar_sitting_forward");
	actor_offAndAnim($klingon2,"bar_sitting_forward");
//SETUP DIALOGS
	thread setOnUseTheread($laptopuser,"dialog_laptopuser");
	thread setOnUseTheread($klingon2,"dialog_klingon2");
	thread setOnUseTheread($blueman,"dialog_blueman");
	thread setOnUseTheread($blueman2,"dialog_blueman2");
	thread setOnUseTheread($seated,"dialog_seated");
	thread setOnUseTheread($dude2,"dialog_dude2");
	thread setOnUseTheread($girl2,"dialog_girl2");
	thread setOnUseTheread($girl,"dialog_girl");
}

void setOnUseTheread(entity e,string s)
//set a thraed to be executed if entity is used
{
	if(doesEntityExist(e))
	{
		e.onuse(s);
		pause();
	}
}

void actor_offAndAnim(entity eActor,string sAnimation)
//disables the ai and animates the actor, makes actor not pushable
//ai must be turned off for animations to work
{
	if(doesEntityExist(eActor))
	{
		eActor.killthread("badKilling");
		eActor.allowfall(1);
		eActor.immortal(0);
		eActor.health(60);
		eActor.archetype("");
		eActor.ai_off();
		eActor.anim(sAnimation);
		eActor.pushable(0);
		eActor.mass(9999999);
		$world.setStringvar("actorTargetnameIndex"+float_actorIndex,""+eActor.getRawtargetname());
		float_actorIndex++;
		wait(0.05);
	}
}

void secretStreetLight()
//toogle dynamic screetlight, indicating the secret
{
	while(1)
	{
		$world.light_intensity("secretStreetLights",1);
		$secretStreetLightBeam.show();
		wait(5);
		$world.light_intensity("secretStreetLights",0);
		$secretStreetLightBeam.hide();
		wait(2);
	}
}

void deskLightToggle()
{//getlightintensity
//toggle desk ligth in the headmaster office
	if($world.getlightintensity("deskLight") ==  0){
		$world.light_intensity("deskLight",1);
		$deskLightBeam.show();
	}else{
		$world.light_intensity("deskLight",0);
		$deskLightBeam.hide();
	}
}


void secretArmorFound()
{
//terminate flickering streetlamp thread
	killthread("secretStreetLight");
//change streetlight
	$world.light_intensity("secretStreetLights",0);
	$secretStreetLightBeam.hide();
	
//activate lamp inside the secret
	$world.light_intensity("secretArmorLight",1);
}

void cinematic_telsiaBeamOut()
//beam telsia on to the enterprise
{
//fade out cam
	cam_fadeOut(0.25,0,0,0,1);
	wait(0.3);
//disable telsia
	$telsia.ai_off();
	$munro.origin('11733.00 -226.00 -736.00');
	$munro.angle(270);
	$munro.show();
	$munro.killattach();
//do cinematic commands only in singleplayer
	if(getCvarInt("g_gametype") != 1)
	{
		cinematic();
		freezeplayer();
		letterbox(0.1);
		$player.hide();
		$player.notsolid();
		$cam1.follow($cinematicSceneC);
		$cam1.cut();
		wait(0.1);
		cuecamera($cam1);
	}
//fade out cam
	cam_fadeIn(0.25,0,0,0,1);
	$telsia.walkto("$munro","walk");
	$munro.walkto("$telsiaBeamOutMunro","walk");
	waitFor($munro);
//make him look at her
	$munro.headwatch($telsia);
//make her look at the player
	$telsia.headwatch($munro);
//make her walk into pistion
	$telsia.walkto("$telsiaBeamOut","walk");
	waitFor($telsia);
//wait a moment to have the player realize what is going on
	wait(0.15);
	$munro.animateonce("tap_comm");
	waitforanimation($munro,"tap_comm");
	$munro.anim("idle");
//dialog - munro to eneterprise, one to beam up
	$munro.playDialog("localization/sound/dialog/dallas02/munro_leavem3.mp3",1,128,0);
//wait for the enterprise response time + dialog
	wait(4);
//beam telsia out
	$telsia.notsolid();
	$telsia.displayeffect("TransportOut","Federation");
//start the shuttle now
	$shuttleOrigin.followpath( $shuttlePath , "ignoreangles" );
//play flyby sound for romulan shuttle
	$shuttle.loopsound("sound/vehicles/shuttlecraft/shuttle_flyby_slow.WAV",1,512);
//wait for the effect to end
	wait(2.7);
//fade out cam
	cam_fadeOut(0.25,0,0,0,1);
	wait(0.3);
//do cinematic commands only in singleplayer
	if(getCvarInt("g_gametype") != 1)
	{
		noncinematic();
		clearletterbox(0.1);
		cueplayer();
		releaseplayer();
		$player.show();
		$player.solid();
	}
	$player.origin($munro.getOrigin());
	$player.turnTowardsEntity($telsia);
//delete her
	$telsia.remove();
	$munro.hide();
//wait a sec
	wait(1);
//Save Game - telsia is safe now
	if(getCvarInt("g_gametype") != 1)
	{
		stuffCMD("ui_savegamewithname \"City4 - Beamed out Teslia\"");
	}
//fade out cam
	cam_fadeIn(0.25,0,0,0,1);
	wait(1);
//start the team reuinite sequence
	thread munro_team();
}

void munro_team()
{
//munro says -> where's my team?
	$player.simplePlayDialog("localization/sound/dialog/m8l2/munro_whereteam3.mp3",1,128);
//move team into position
	$korban.walkto("$getTogether_korban","run");
//wait for korban to show up
	waitfor($korban);
//Korban look at player
	$korban.headWatch($player);
//delay
	wait(0.1);
//dialog -> why you go alone, blah, the leader should not take point
	$korban.playDialog("localization/sound/dialog/m8l1/korban_takepoint.mp3",2,256,0);
//make the team follow munro
	$korban.settendency("follow",1);
	$korban.ai_on();
	$chang.walkto("$getTogether_chang","run");
	waitfor($chang);
	$chang.settendency("follow",1);
	$chang.ai_on();
}

void shuttleAttacked()
//if romulan shuttle has been attacked by the player
{
//make korban and chang able to die
	$korban.immortal(0);
	$korban.health(40);
	$chang.immortal(0);
	$chang.health(40);
	wait(0.1);
	$shuttle.loopsound("sound/ships/romulan/rom_redalert.wav",1,512);
	wait(0.1);
//beam in lots of romulan troops that will defently kill
//the player and his team
	float	fBeamInLoactaion;
	float	fBeamedIn;
	entity	eSpawn;
	for(fBeamInLoactaion=0;fBeamInLoactaion<20;fBeamInLoactaion++)
	{
		fBeamedIn++;
		eSpawn = getEntity("troopBeamLocation"+fBeamInLoactaion);
		if(doesEntityExist(eSpawn))
		{
			eSpawn.spawneffectname("TransportIn","Romulan");
			triggerEntity(eSpawn);
		}
		if(fBeamedIn > 3)
		{
			fBeamedIn=0;
			wait(0.5);
		}
		
		wait(0.05);
	}
}

void beamInSomeone()
//beam someone in and then go sit at a table
{
	wait(0.1);
	$anika.killthread("badKilling");
	$anika.health(60);
	$anika.pushable(0);
	$anika.mass(9999999);
	$anika.nodamage();
	$anika.ai_off();
	wait(2.8);
	$anika.walkto("$anikaTargetDest","walk");
	if(!float_playerIsAMurderer)
	{
		waitFor($anika);
		$anika.angle(270);
		$anika.origin('10395 -800 -440.00');//10396.00 -789.00 -440.00
		$anika.anim("bar_sitting_forward");
		
		if(getCvarInt("g_gametype") != 1)
		{
			$player.setobjectiveshow( "c4sitdownAnd", 1 );
		}
	}
}

void animations()
{
// Andorian 1 - sitting in Cafe
	$blueman.immortal( 1 );
	$blueman.ai_off();
	$blueman.anim( "bar_sitting_forward" );
	$blueman.pushable( 0 );
	
// Andorian 2 - sitting in Cafe
	$blueman2.immortal( 1 );
	$blueman2.ai_off();
	$blueman2.anim( "bar_sitting_forward" );
	$blueman2.pushable( 0 );
	
// Man 1 - sitting in Cafe
	$dude.immortal( 1 );
	$dude.ai_off();
	$dude.anim( "bar_sitting_forward" );
	$dude.pushable( 0 );
	
// Man 2 - sitting in Cafe
	$dude2.immortal( 1 );
	$dude2.ai_off();
	$dude2.anim( "bar_sitting_forward" );
	$dude2.pushable( 0 );
	
// klingon - sitting in Cafe
	$klingon1.immortal( 1 );
	$klingon1.ai_off();
	$klingon1.anim( "bar_sitting_forward" );
	$klingon1.pushable( 0 );
	
// klingon 2 - sitting in Cafe
	$klingon2.immortal( 1 );
	$klingon2.ai_off();
	$klingon2.anim( "bar_sitting_forward" );
	$klingon2.pushable( 0 );
	
// Girl - sitting in Cafe
	$seated.immortal( 1 );
	$seated.ai_off();
	$seated.anim( "bar_sitting_forward" );
	$seated.pushable( 0 );
	
// Girl at Laptop - sitting in lobby
	$laptopuser.immortal( 1 );
	$laptopuser.ai_off();
	$laptopuser.anim( "bar_sitting_relaxed" );
	$laptopuser.pushable( 0 );
	
// Girl at table on upstairs
	$girl.immortal( 1 );
	$girl.ai_off();
	$girl.anim( "bar_sitting_relaxed" );
	$girl.pushable( 0 );
	
// Girl at table on upstairs
	$girl2.immortal( 1 );
	$girl2.ai_off();
	$girl2.anim( "bar_sitting_forward" );
	$girl2.pushable( 0 );
	
//Animate laptop to be opened
	$laptop1.anim("open_idle");
}

void klingon2()
{
//Klingons talk
	$klingon2.headWatch($player);
	wait(0.2);
	$klingon2.playDialog("localization/sound/dialog/m9l1/klingon_gagh2.mp3",2,128,0);
	waitForDialog( $klingon2 );
	
	$klingon1.headWatch($klingon2);
	$klingon2.headWatch($klingon1);
	wait(0.2);
	$klingon1.playDialog("localization/sound/dialog/m9l1/merc3_tellingyou.mp3",2,128,0);
}

void spy()
{
//Spy stuff
	$spy.playDialog("localization/sound/dialog/m10l1/info_download.mp3",2,128,0); //download
	waitForDialog( $spy );
	
	$attrex.playDialog("localization/sound/dialog/m7l1/attrexian2_hearsome.mp3",2,128,0); //I hear something
	waitForDialog( $attrex );
	
	//Save Game
	stuffCMD("ui_savegamewithname \"City - Cafe\"");
}

void chang_stay()
{
//munro says -> chnag stay behind and secure the area, while I do a chicken dance
	$player.simplePlayDialog("localization/sound/dialog/m7l1/munro_changsecure.mp3",1,128);	
	
//wait for dialog
	wait(3);
	
//chang says -> Yes Mam
	$chang.playDialog("localization/sound/dialog/combat/chang_yessir9.mp3",2,128,0);
	
//make Chang and Korban no more follow munro on twitter
	$korban.settendency("follow",0);
	$chang.settendency("follow",0);
}
	

void found_spy()
{
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectiveshow( "c4followSpy", 1 );
	}
	$korban.settendency("follow",0);
	$chang.settendency("follow",0);
//open door for escape
	$doorToGarden.open($world);
//make spy look at munro
	$spy.headwatch($player);
	$spy.turnTowardsEntity($player);
	$spy.anim("idle_breathing");
	wait(0.15);
//Unlock the door!
	$doorToSpy.unlock();
	$doorToSpy.open($spy);
//make spy run!
	$spy.walkto("$spyRunTo1","run");
	waitFor($spy);
	$doorToSpy.close();
	$doorToSpy.lock();
	$spy.walkto("$spyRunTo2","run");
	waitFor($spy);
	$korban.settendency("follow",1);
	$chang.settendency("follow",1);
	$spy.walkto("$spyRunTo3","run");
	waitFor($spy);
	$spy.walkto("$spyEscape","run");
	waitFor($spy);
	$spy.remove();
	
	wait(5);
	
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectiveshow( "c4scannArea", 1 );
	}
}

void areaScanned()
//player has sucessfully scanned the area with the tricorder
{
//show holo emitters
	$holoEmitter1.missionobjective(1);
	$holoEmitter2.missionobjective(1);
	$holoEmitter1.show();
	$holoEmitter2.show();
//remove targeting texture
	$tricorderArea1.remove();
//remove tricorder area indicator
	$tricorderSymbol1.remove();
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectivecomplete( "c4scannArea", 1 );
	}
}

void holoEmitterBlow1()
//holoemitter destroyed
{
//show sparks, and emitter
	$holoEmitter1.show();
	$holoEmitter1Spark.show();
	$holoEmitter1.selfdetonate();
	$holoEmitter1.missionobjective(0);
	if(float_holoEmittersDamaged==0)
	{
		float_holoEmittersDamaged++;
	//make holograms flicker
		thread hologramsFlicker();
	}
	else
	{
	//hide holograms
		$romTrigger.triggerable();
		$doorPannel.show();
		killthread("hologramsFlicker");
		$hologramBush.remove();
		$hologramDoor.remove();
		$areaScannPuzzle.remove();
		$tricorderArea1.remove();
		$tricorderSymbol1.remove();
	}
}

void holoEmitterBlow2()
//holoemitter destroyed
{
//show sparks, and emitter
	$holoEmitter2.show();
	$holoEmitter2Spark.show();
	$holoEmitter2.selfdetonate();
	$holoEmitter2.missionobjective(0);
	if(float_holoEmittersDamaged==0)
	{
		float_holoEmittersDamaged++;
	//make holograms flicker
		thread hologramsFlicker();
	}
	else
	{
	//hide holograms
		$romTrigger.triggerable();
		$doorPannel.show();
		killthread("hologramsFlicker");
		$hologramBush.remove();
		$hologramDoor.remove();
		$areaScannPuzzle.remove();
		$tricorderArea1.remove();
		$tricorderSymbol1.remove();
	}
}

void hologramsFlicker()
{
	$hologramDoor.loopsound("sound/ships/drull/drull_hologram.wav",1,256);
	while(1)
	{
		$hologramDoor.hide();
		$hologramBush.hide();
		wait(0.15);
		$hologramBush.show();
		$hologramDoor.show();
		wait(0.15);
	}
}

void openDoors()
//open the doors outwards
{
	$t191.notsolid();
	$t191.open($teleportPad1);
}

void shatterWindow()
{
	$player.playsound("sound/impact/explosion/explo_neonsign.wav",0,1,128);
}

void badKilling()
//if player kills the folks, they all atempt to kill him
{
	float_playerIsAMurderer=1;
//enable puzzle to hack and show area
	$hackingDoorPuzzle.puzzleobject_activate();
	$tricorderArea2.show();
//remove the non violant solution
	$playerSitMarker.remove();
	$cinematic_playerSitDownTrigger.remove();
	
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectivefailed( "c4avoidConflict", 1 );
		$player.setobjectivefailed( "c4sitdownAnd", 1 );
	}
	
	entity eActor;
	string sTargetname;
	for(float_actorIndex=float_actorIndex;float_actorIndex > -2;float_actorIndex--)
	{
		sTargetname = $world.getStringvar("actorTargetnameIndex"+float_actorIndex);
		
		if(float_actorIndex == -1)
		{
			sTargetname	= "anika";
		}
		
		if(sTargetname != "")
		{
			eActor = getEntity(sTargetname);
			if(doesEntityExist(eActor))
			{
				if(eActor.getHealth() > 0)
				{
					eActor.aggressive(1);//get aggressive
					//eActor.attack($player);//attack player
					eActor.actortype("enemy");//they are your enemy now
					eActor.health(40);//make them die relativley quickley
					eActor.immortal(0);//make em able to die
					eActor.ai_on();//enable ai
					eActor.killthread("");//execute killthread just once
					eActor.nouse();//don't play dialog on use anymore
				//replace with hostile counterparts
					if(sTargetname == "blueman" || sTargetname == "blueman2")
					{
						eActor.model("models/char/andorian-m9-merc-rocket-m1.tik");
					}
					else if(sTargetname == "dude2")
					{
						eActor.model("models/char/human-m9-merc-shotgun-m1.tik");
					}
					else if(sTargetname == "seated" || sTargetname == "girl")
					{
						eActor.model("models/char/human-m9-merc-pistol-f1.tik");
					}
					else if(sTargetname == "laptopuser" || sTargetname == "girl2")
					{
						eActor.model("models/char/human-m9-merc-pistol-f2.tik");
					}
					else if(sTargetname == "klingon2")
					{
						eActor.model("models/char/klingon-m9-merc-knife-m1.tik");
					}
				}
			}
		}
	//make korban and chang able to die
		$korban.immortal(0);
		$korban.health(70);
		$chang.immortal(0);
		$chang.health(70);
	}
}

void cinematic_playerSitDown()
//player sits down to wait for romulan
{
//fade screen from black back in again to normal
	cam_fadeOut(0.25,0,0,0,1);
	wait(0.3);
	$munro.origin('10611.00 -333.00 -440.00');
	$munro.angle(360);
	$munro.show();
	$munro.killattach();
	$playerSitMarker.remove();
//do cinematic commands only in singleplayer
	if(getCvarInt("g_gametype") != 1)
	{
		cinematic();
		freezeplayer();
		letterbox(0.1);
		$player.hide();
		$player.notsolid();
		$cam1.follow($cinematicSceneA);
		$cam1.cut();
		wait(0.1);
		cuecamera($cam1);
	}
	
	$korban.ai_off();
	$korban.notsolid();
	$korban.hide();
	$chang.ai_off();
	$chang.notsolid();
	$chang.hide();
	
//fade screen from black back in again to normal
	cam_fadeIn(0.25,0,0,0,1);
	wait(0.2);
	$munro.animateonce("ent-seta_sit-down");
	waitforanimation($munro,"ent-seta_sit-down");
	$munro.anim("ent-seta_sit-idle");
	wait(0.2);
	$munro.animateonce("ent-seta_sit-idle-look");
	waitforanimation($munro,"ent-seta_sit-idle-look");
	
	$munro.animateonce("ent-seta_sit-gesture");
	wait(0.3);
	
	$munro.anim("ent-seta_sit-idle");
	wait(0.02);
	$munro.watchEntity($doorToPrivateBar);

	if(getCvarInt("g_gametype") != 1)
	{
		$cam2.follow($cinematicSceneB);
		$cam2.cut();
		wait(0.1);
		cuecamera($cam2);
	}
//spawn romulan
	triggerEntity($spawn_transporterRomulan);
//unlock and open the door
	$doorToPrivateBar.unlock();
	$doorToPrivateBar.open($spawn_transporterRomulan);
	wait(0.1);
	$transporterRomulan.ai_off();
	
	$transporterRomulan.walkto("$teleportPad2","walk");
	waitFor($transporterRomulan);
	$transporterRomulan.displayeffect("TransportOut","Federation");
	$transporterRomulan.notsolid();
	$transporterRomulan.notarget();
	wait(1.8);
	
	cam_fadeOut(0.25,0,0,0,1);
	wait(.3);
	$munro.hide();
	
//do cinematic commands only in singleplayer
	if(getCvarInt("g_gametype") != 1)
	{
		$player.setobjectivecomplete( "c4sitdownAnd", 1 );
		noncinematic();
		clearletterbox(0.1);
		cueplayer();
		releaseplayer();
		$player.show();
		$player.solid();
	}
	
	$korban.solid();
	$korban.show();
	$korban.ai_on();
	$chang.show();
	$chang.solid();
	$chang.ai_on();
	
	cam_fadeIn(0.25,0,0,0,1);
	wait(1);
	$transporterRomulan.remove();
}


void replicatedFood()
{
	$replicatedFoodTrigger.playsound("sound/ships/enterprise/ent_replicator.wav",3,2,64);
	wait(1);
	$replicatedFoodTrigger.triggerable();
}

void replicatedFoodEat()
{
	$replicatedFood.remove();
//give +25 health
	$player.health(($player.getHealth() + 25));
	$replicatedFoodTrigger.playsound("sound/player/player_gulp.wav",3,4,128);
}

void openDoorToGarden()
//opens the door to the garden
{
	$doorToGarden.open($world);
}

void secretBarWakeEmUp()
{
	$barSoldier0.attack($player);
	$barSoldier1.attack($player);
	$barSoldier2.attack($player);
	$barSoldier3.attack($player);
}

void dialog_laptopuser()
{
	$laptopuser.nouse();
	$laptopuser.headWatch($player);
	$laptopuser.playdialog("localization/sound/dialog/m9l1/bar_romale.mp3",1,256,0);
	waitForDialog($laptopuser);
	$laptopuser.headWatchtarget("none",20);
}
void dialog_klingon2()
{
	$klingon2.nouse();
	$klingon2.headWatch($player);
	$klingon1.headWatch($player);
	$klingon2.playdialog("localization/sound/dialog/m9l1/merc5_bloodw.mp3",1,256,0);
	waitForDialog($klingon2);
	$klingon1.playdialog("localization/sound/dialog/m9l1/bar_bev.mp3",1,256,0);
	waitForDialog($klingon1);
	$klingon2.headWatchtarget("none",20);
	$klingon1.headWatchtarget("none",20);
}
void dialog_blueman()
{
	$blueman.nouse();
	$blueman.headWatch($player);
	$blueman.playdialog("localization/sound/dialog/m9l1/alien_stepaway.mp3",1,256,0);
	waitForDialog($blueman);
	$blueman.headWatchtarget("none",20);
}
void dialog_blueman2()
{
	$blueman2.nouse();
	$blueman2.headWatch($player);
	$blueman2.playdialog("localization/sound/dialog/m9l1/merc5_startfight.mp3",1,256,0);
	waitForDialog($blueman2);
	$blueman2.headWatchtarget("none",20);
}
void dialog_dude2()
{
	$dude2.nouse();
	$dude2.headWatch($player);
	$dude2.playdialog("localization/sound/dialog/m9l1/merc5_vulcport.mp3",1,256,0);
	waitForDialog($dude2);
	$dude2.headWatchtarget("none",20);
}

void dialog_girl2()
{
	$girl2.nouse();
	$girl2.headWatch($player);
	$girl2.playdialog("localization/sound/dialog/m2l1/female_guyjerk.mp3",1,256,0);
	waitForDialog($girl2);
	$girl2.headWatchtarget("none",20);
}
void dialog_girl()
{
	$girl.nouse();
	$girl.headWatch($player);
	$girl.playdialog("localization/sound/dialog/m2l1/female_youguy.mp3",1,256,0);
	waitForDialog($girl);
	$girl.headWatchtarget("none",20);
}
void dialog_seated()
{
	$seated.nouse();
	$seated.headWatch($player);
	$seated.playdialog("localization/sound/dialog/m2l1/femc2_dontmet.mp3",1,256,0);
	waitForDialog($seated);
	$seated.headWatchtarget("none",20);
}

void fail_lostTeammate()
//korban or chang killed
{
	$player.missionfailed("$$lostTeamMate$$");
}

void doorHacked()
//puzzle hacked to seceret bar, needs player to kill the pople in the bar
{
	$doorToPrivateBar.unlock();
	$doorToPrivateBar.open($player);
	$tricorderArea2.remove();
	$hackingDoorPuzzle.remove();
}


void doorHackedUsed()
{
	$tricorderArea2.remove();
}

void doorHackedSpy()
//puzzle hacked to follow thy spy
{
	$doorToSpy.unlock();
	$doorToSpy.open($player);
	$hackingDoorPuzzleSpy.remove();
	$tricorderArea3.remove();
}

void noAccess()
{
	entity e;
	e=getCurrentEntity();
	if(doesEntityExist(e))
	{
		e.playsound("",3,1,256);
	}
	centerprint("$$accessDenied$$");
}

void noAccessYet()
{
	entity e;
	e=getCurrentEntity();
	if(doesEntityExist(e))
	{
		e.playsound("",3,1,256);
	}
	centerprint("$$accessNotYet$$");
}

void exitNow()
{
//don't display MISSION SUCESS screen
	nointermission();
}

void changeLevel()
{
}

